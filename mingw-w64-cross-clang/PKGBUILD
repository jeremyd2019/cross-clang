declare -g -A _cross_arches=(
  ["/clang64"]="x86_64-w64-mingw32"
  ["/clang32"]="i686-w64-mingw32"
  ["/clangarm64"]="aarch64-w64-mingw32"
)
pkgbase=mingw-w64-cross-clang
pkgname=($(for _pfx in "${!_cross_arches[@]}"; do
             if [[ "${_cross_arches[$_pfx]%%-*}" != "${CARCH}" ]]; then
               echo "${MINGW_PACKAGE_PREFIX}-cross-clang-${_cross_arches[$_pfx]%%-*}"
             fi
           done))
pkgver=14.0.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('ISC')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
# this is unused in the packages but necessary to make CI happy
depends=("${MINGW_PACKAGE_PREFIX}-clang=${pkgver}"
         "${MINGW_PACKAGE_PREFIX}-cross-compiler-rt=${pkgver}")
source=("native-wrapper.h"
        "llvm-wrapper.c"
        "clang-target-wrapper.c"
        "dlltool-wrapper.c"
        "windres-wrapper.c")
sha256sums=('c9758342cb926605cd1b30ccb92e6b47f5cc930a15904462e0445959f9be49c1'
            'd6bafc72309860e7a45a91047b5e4c4519b52d6d685069f77094ef6cc09385e5'
            'f5a03acc5fb0536a59b3bb5c8ff967604311d7e44182c8f75a0cf1c732b05b6c'
            '2adee7d81d71a167d28fc508e8b3427f63e7c064f08ca63fe330476e8ae4a2c8'
            'b121e52bc752396f887e8e6a97138117fc9c2ee4f7efbe9f5ca2be4f451b6f45')
build() {
  CC=${CC:-cc}
  cd "${srcdir}"

  [[ -d "build-${MSYSTEM}" ]] && rm -rf "build-${MSYSTEM}"
  mkdir "build-${MSYSTEM}" && cd "build-${MSYSTEM}"

  for _pfx in "${!_cross_arches[@]}"; do
    if [[ "${_cross_arches[$_pfx]%%-*}" != "${CARCH}" ]]; then
      MSYS2_ARG_CONV_EXCL="-DSYSROOT=" \
      $CC $CPPFLAGS $CFLAGS $LDFLAGS -municode -DDEFAULT_TARGET="\"${_cross_arches[$_pfx]}\"" -DSYSROOT="\"${_pfx}\"" ../llvm-wrapper.c -o ${_cross_arches[$_pfx]}-llvm-wrapper.exe
      MSYS2_ARG_CONV_EXCL="-DSYSROOT=" \
      $CC $CPPFLAGS $CFLAGS $LDFLAGS -municode -DDEFAULT_TARGET="\"${_cross_arches[$_pfx]}\"" -DSYSROOT="\"${_pfx}\"" ../clang-target-wrapper.c -o ${_cross_arches[$_pfx]}-clang.exe
      MSYS2_ARG_CONV_EXCL="-DSYSROOT=" \
      $CC $CPPFLAGS $CFLAGS $LDFLAGS -municode -DDEFAULT_TARGET="\"${_cross_arches[$_pfx]}\"" -DSYSROOT="\"${_pfx}\"" ../dlltool-wrapper.c -o ${_cross_arches[$_pfx]}-dlltool.exe
      MSYS2_ARG_CONV_EXCL="-DSYSROOT=" \
      $CC $CPPFLAGS $CFLAGS $LDFLAGS -municode -DDEFAULT_TARGET="\"${_cross_arches[$_pfx]}\"" -DSYSROOT="\"${_pfx}\"" ../windres-wrapper.c -o ${_cross_arches[$_pfx]}-windres.exe
    fi
  done
}
_real_package() {
  local _pfx="@@@PREFIX@@@"
  depends=("${MINGW_PACKAGE_PREFIX}-clang=${pkgver}"
           "${MINGW_PACKAGE_PREFIX}-cross-compiler-rt=${pkgver}"
           "${MINGW_PACKAGE_PREFIX}-llvm=${pkgver}"
           "${MINGW_PACKAGE_PREFIX}-lld=${pkgver}"
           "mingw-w64-clang-${_cross_arches[@@@PREFIX@@@]%%-*}-crt"
           "mingw-w64-clang-${_cross_arches[@@@PREFIX@@@]%%-*}-headers"
           "mingw-w64-clang-${_cross_arches[@@@PREFIX@@@]%%-*}-libc++=${pkgver}"
           "mingw-w64-clang-${_cross_arches[@@@PREFIX@@@]%%-*}-libc++abi=${pkgver}"
           "mingw-w64-clang-${_cross_arches[@@@PREFIX@@@]%%-*}-libunwind=${pkgver}"
           "mingw-w64-clang-${_cross_arches[@@@PREFIX@@@]%%-*}-winpthreads-git")

  cd "${srcdir}/build-${MSYSTEM}"

  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  local _tool
  # c11 c99 ?
  for _tool in as c++ cc clang clang++ gcc g++; do
    cp -f ${_cross_arches[$_pfx]}-clang.exe "${pkgdir}${MINGW_PREFIX}/bin/${_cross_arches[$_pfx]}-${_tool}.exe"
  done
  for _tool in addr2line ar ranlib nm objcopy readelf strings strip; do
    cp -f ${_cross_arches[$_pfx]}-llvm-wrapper.exe "${pkgdir}${MINGW_PREFIX}/bin/${_cross_arches[$_pfx]}-${_tool}.exe"
  done
  # windres and dlltool can't use llvm-wrapper, as that loses the original
  # target arch prefix
  for _tool in dlltool windres; do
    cp -f ${_cross_arches[$_pfx]}-${_tool}.exe "${pkgdir}${MINGW_PREFIX}/bin/${_cross_arches[$_pfx]}-${_tool}.exe"
  done

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/licenses/${_cross_arches[${_pfx}]}-cross-clang"
  sed -ne '1,/^ \*\//p' "${srcdir}/native-wrapper.h" > "${pkgdir}${MINGW_PREFIX}/share/licenses/${_cross_arches[${_pfx}]}-cross-clang/LICENSE"
}

_func="$(declare -f "_real_package")"
for _pfx in "${!_cross_arches[@]}"; do
  if [[ "${_cross_arches[$_pfx]%%-*}" != "${CARCH}" ]]; then
    _func2="${_func//@@@PREFIX@@@/${_pfx}}"
    eval "${_func2/#_real_package/package_${MINGW_PACKAGE_PREFIX}-cross-clang-${_cross_arches[$_pfx]%%-*}}"
  fi
done
